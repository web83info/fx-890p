100 CLEAR:CLS:V=0.56
110 LOCATE5,1:PRINT"1996 (C) ABCP software":WAIT7
120 FORI=0TO3:DEFCHR$(252+I)=MID$("FFFFFDFDF1FFFFD5ABD5ABFFFFA59999A5FFFF81BDBD81FF",I*12+1,12):NEXT
130 CLS:MODE50,(58,6)-(132,16):LOCATE10,1:PRINT"Panic  Maker"
140 I=0:FORJ=0TO0:I=1-I:POKE&H1775,32*I+7:LOCATE8,3:PRINT" PUSH SPACE KEY ";:FORL=1TO10:J=J-(INKEY$=" "):NEXT:J=J+(J=0):NEXT
150 CLS:INPUT"Level=",R$:R=VAL(R$):IF R<=0 OR R<>INTR OR R>100 GOTO 150
160 CLS:ERASEM$:DIM M$(4):POKE&H1775,5:FORI=0TO3
170 L=INT(RAN#*4):IF M$(L)<>"" GOTO170
180 FORJ=0TO3:M$(L)=M$(L)+CHR$(252+I):NEXT:NEXT:U=0:U$="":S=0
190 FORI=1TOR:J=INT(RAN#*4):GOSUB*TATEM:J=INT(RAN#*4):GOSUB*YOKOM:LOCATE10,1:PRINTUSING"Finished ###%";I/R*100:NEXT
200 CLS:LOCATE0,1:PRINT"Panic Maker":LOCATE4,2:PRINTUSING"Ver#.##";V
210 LOCATE20,2:PRINT"Presented":LOCATE24,3:PRINT"By ABCP";
220 I$=CHR$(135):FORI=0TO3:LOCATE11,I:PRINTI$CHR$(48+I+5);:LOCATE18,I:PRINTI$;:NEXT
230 LOCATE21,0:PRINTUSING"Level:###";R:LOCATE21,1:PRINT"Steps:  0"
240 FORI=0TO3:LOCATE13,I:PRINTM$(I);:NEXT
250 I$=INKEY$:I$=INPUT$(1):K=ASC(I$):IF K=ASC("G") GOTO150 ELSE IF K=13 THEN GOSUB*CHECK ELSE IF K<&H31 OR K>&H39 GOTO250
260 J=K-&H31:IF J=8 THEN GOSUB*UNDO ELSE IF LEN(U$)<255 THEN U$=U$+I$ ELSE250
270 IF J<=3 THEN GOSUB*TATE ELSE IF J<=7 THEN J=J-4:GOSUB*YOKO
280 S=S+1:LOCATE27,1:PRINTUSING"###";S:GOTO240
290 *CHECK
300 F=0:FORL=0TO3:FORI=1TO3:IFMID$(M$(L),I,1)=MID$(M$(L),I+1,1)THEN F=F+1
310 NEXT:NEXT:IF F=12 THEN RETURN*OK
320 F=0:FORI=1TO4:FORL=0TO2:IFMID$(M$(L),I,1)=MID$(M$(L+1),I,1)THEN F=F+1
330 NEXT:NEXT:IF F=12 THEN RETURN*OK
340 MODE50,(82,6)-(94,16):MODE51,(83,7)-(95,15):LOCATE14,1:PRINT"NG":WAIT7:RETURN240
350 *OK:MODE50,(70,6)-(108,16):MODE51,(71,7)-(107,15):LOCATE12,1:PRINT"CLEAR!"
360 I$=INKEY$:I$=INPUT$(1):IF I$<>" " GOTO360 ELSE IF R<100 THEN R=R+1
370 GOTO160
380 *YOKO:M$(J)=RIGHT$(M$(J),1)+LEFT$(M$(J),3):RETURN
390 *TATE:M$(4)=M$(3):FORL=3TO0STEP-1:M$(L)=LEFT$(M$(L),J)+MID$(M$(L-1-(L=0)*5),J+1,1)+RIGHT$(M$(L),3-J):NEXT:RETURN
400 *YOKOM:M$(J)=RIGHT$(M$(J),3)+LEFT$(M$(J),1):RETURN
410 *TATEM:M$(4)=M$(0):FORL=0TO3:M$(L)=LEFT$(M$(L),J)+MID$(M$(L+1),J+1,1)+RIGHT$(M$(L),3-J):NEXT:RETURN
420 *UNDO:U=LEN(U$):IF U=0 THEN S=S-1:RETURN
430 J=ASC(RIGHT$(U$,1))-&H31:U$=LEFT$(U$,U-1):S=S-2:IF J<=3 THEN GOSUB*TATEM ELSE J=J-4:GOSUB*YOKOM
440 RETURN280
