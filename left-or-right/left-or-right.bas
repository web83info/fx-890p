100 '**************************
110 'Left or Right ver.1.1
120 '(C) 1996 ABCP software
130 '**************************
140 DEFSEG=0:POKE &H1775,5:CLS:CLEAR:A$=" Left":B$="Right ":C$="or"
150 FOR I=0 TO 9:LOCATE I,0:PRINT A$C$:WAIT 1:NEXT
160 FOR I=18 TO 25:LOCATE 43-I,0:PRINT B$:NEXT
170 FOR I=0 TO 2:LOCATE 9+(I=1),0:PRINT A$C$:FOR J=0 TO 50:NEXT:NEXT
180 LOCATE 14,0:PRINT " "C$
190 LOCATE 9,3:PRINT REV" PUSH ANY KEY ";
200 I$=INKEY$:I$=INPUT$(1)
210 CLS:I$=CHR$(29)+CHR$(29)+CHR$(31):B$=CHR$(252)+CHR$(253)+I$+CHR$(254)+CHR$(255):C$="  "+I$+"  ":S=0
220 A$="10101013161CF8F0D01010102141818181FFFF818181810110101013161CF8F0D01010102040FFFF828282828282FFFF"
230 MODE 50,(18,4)-(42,27):MODE 50,(66,4)-(88,27)
240 FOR I=0 TO 2:LOCATE 20,1:PRINT MID$("Ready Set  Go!",I*5+1,5):WAIT 6:NEXT
250 LOCATE 20,1:PRINT "SCORE:    0"
260 LOCATE 21,2:PRINT USING "HIGH:#####";H
270 TIMER=0
280 Q=INT(RAN#*2):P=INT(RAN#*2)
290 FOR J=0 TO 3:DEFCHR$(252+J)=MID$(A$,J*12+1+Q*48,12):NEXT
300 LOCATE 4+P*8,1:PRINT B$
310 J=I:I=ASC(INKEY$):IF TIMER>300 GOTO 350 ELSE IF I=J OR ABS(I-44)-1 GOTO 310
320 IF (P=Q)*(I=43)+(P-Q)*(I=45) THEN S=S+10 ELSE S=S-20
330 LOCATE 26,1:PRINT USING "#####";S
340 LOCATE 4+P*8,1:PRINT C$:GOTO 280
350 MODE 50,(4,6)-(48,16):MODE 51,(5,7)-(47,15)
360 LOCATE 1,1:PRINT "TIME UP"
370 IF H<S THEN H=S:MODE 50,(52,14)-(114,24):MODE 51,(53,15)-(113,23):LOCATE 9,2:PRINT "HIGH SCORE";
380 I$=INKEY$:I$=INPUT$(1):IF I$<>CHR$(13) GOTO 380 ELSE 210
