1 'Number Breaker for FX-890P/PC-E200/PC-E500 (BASIC version)
100 CLS :CLEAR :DIM P(9),Q(9)
110 IF STR$ (0)="0" THEN Z=24:GOTO 130
120 IF HEX$ (0)="0" THEN Z=40 ELSE Z=32
130 IF Z-32 THEN RANDOMIZE
140 LOCATE (Z-18)/2,1:PRINT "<<Number Breaker>>"
150 LOCATE (Z-14)/2,3:PRINT "PUSH SPACE KEY";
160 IF INKEY$<>" " THEN 160
170 CLS :LOCATE (Z-21)/2,2:PRINT "Same Swap Large Small"
180 N=1:B$="????"
190 IF Z=24 THEN A$=RIGHT$ ("0000"+STR$ (RND(10000)),4):GOTO 220
200 IF Z=32 THEN R=INT(RAN#*10000) ELSE R=RND(10000)
210 A$=RIGHT$ ("0000"+MID$ (STR$ (R),2,LEN (STR$ (R))-1),4)
220 LOCATE (Z-15)/2,0:PRINT LEFT$ (B$+"0000",4)+" =>"+RIGHT$ (" "+STR$ (N),2)+MID$ ("stndrdth",N*2-1+(N>4)*(N*2-8),2)+":    "
230 LOCATE (Z-15)/2+12,0:INPUT"",B$
240 M=0:W=0:L=0:S=0:FOR I=0 TO 9:P(I)=0:Q(I)=0:NEXT
250 FOR I=1 TO 4:P=VAL (MID$ (A$,I,1)):Q=VAL (MID$ (B$,I,1)):M=M-(P=Q):S=S-(P>Q):L=L-(P<Q):IF P-Q THEN P(P)=P(P)+1:Q(Q)=Q(Q)+1
260 NEXT :FOR I=0 TO 9:P=P(I):Q=Q(I):IF P>0 AND Q>0 THEN W=W+(P+(P>Q)*(P-Q))
270 NEXT
280 Q=(Z-21)/2:LOCATE Q+1,3:PRINT M;:LOCATE Q+6,3:PRINT W;:LOCATE Q+11,3:PRINT L;:LOCATE Q+17,3:PRINT S;
290 IF M-4 AND N<10 THEN N=N+1:GOTO 220
300 IF M=4 THEN LOCATE (Z-12)/2,1:PRINT "[GAME CLEAR]"
310 IF M-4 THEN LOCATE (Z-16)/2,1:PRINT "[TURN OVER:"+A$+"]"
320 IF INKEY$<>" " THEN 320
330 GOTO 170
