10 CLS:CLEAR:V=1
20 LOCATE 11,1:PRINT "»Ý½³ Ï½À-":LOCATE 4,3:PRINT REV" PUSH SPACE KEY TO START ";
30 IF INKEY$<>" " GOTO 30
40 CLS:INPUT "LEVEL";L$:IF L$="" THEN 60
50 V=VAL(L$):IF V-INT(V) OR V>20 OR V<1 GOTO 40
60 DIM M(5,3),M$(3):X=7
70 FOR I=0 TO 3
80 M(0,I)=INT(RAN#*9+1):M(1,I)=INT(RAN#*4+1):M(2,I)=INT(RAN#*9+1):IF (M(1,I)=2 AND M(0,I)<M(2,I)) OR (M(1,I)=2 AND M(2,I)=0) GOTO 80
90 NEXT:GOSUB *CAL
100 FOR I=0 TO 3:M(3,I)=M(5,I)\10:M(4,I)=M(5,I) MOD 10:NEXT
110 FOR I=1 TO V*2:N=INT(RAN#*5):ON INT(RAN#*2+1) GOSUB *UP,*DOWN:NEXT:XX=7:GOSUB *CAL:GOSUB *CHK:IF C=4 GOTO 100
120 CLS:GOSUB *SCR
130 FOR Y=0 TO 3:LOCATE 22,Y:PRINT M$(Y);:NEXT:LOCATE X-1,0:PRINT "["CHR$(28)"]":IF C=4 GOTO *FINISH
140 POKE 6005,5:I$=INPUT$(1):IF I$="4" AND X>7 THEN X=X-2 ELSE IF I$="6" AND X<17 THEN X=X+2
150 IF XX-X THEN LOCATE XX-1,0:PRINT " "CHR$(28)" ":XX=X:GOTO 130
160 N=(X-7)/2:IF N=3 GOTO 130
170 N=N+(N>=3):IF I$="8" THEN GOSUB *UP ELSE IF I$="2" THEN GOSUB *DOWN
180 GOSUB *CAL:FOR Y=0 TO 3:GOSUB *INFO:NEXT:GOSUB *CHK:GOTO 130
190 *FINISH:LOCATE 9,1:PRINT "Well done!":MODE 50,(52,6)-(113,16):WAIT 15:GOSUB *SCR
200 IF INKEY$<>CHR$(13) GOTO 200 ELSE 40
210 *CHK:C=0:FOR I=0 TO 3:C=C-(M$(I)="OK"):NEXT:RETURN
220 *CAL:FOR I=0 TO 3:J=VALF(STR$(M(0,I))+MID$("+-*/",M(1,I),1)+STR$(M(2,I))):M(5,I)=J:IF INT(J)=M(3,I)*10+M(4,I) THEN M$(I)="OK" ELSE M$(I)="NO"
230 NEXT:RETURN
240 *INFO:IF N-1 THEN LOCATE 6+2*N-(N>=3)*2,Y:PRINT M(N,Y); ELSE LOCATE 9,Y:PRINT MID$("+-"+CHR$(240)+CHR$(159),M(1,Y),1);
250 RETURN
260 *SCR:FOR Y=0 TO 3:FOR N=0 TO 4:GOSUB *INFO:NEXT:LOCATE 13,Y:PRINT "=";:LOCATE 18,Y:PRINT "  [ ";M$(Y);" ]";:NEXT:RETURN
270 *UP:L=M(N,0):FOR J=0 TO 2:M(N,J)=M(N,J+1):NEXT:M(N,3)=L:RETURN
280 *DOWN:L=M(N,3):FOR J=3 TO 1 STEP -1:M(N,J)=M(N,J-1):NEXT:M(N,0)=L:RETURN
